plugins {
    id 'com.android.application'
    // Requires either Gradle-7.3.3 or gradlePluginPortal buildscript repository
    id "com.mikepenz.aboutlibraries.plugin" version "10.1.0"
}

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.viliussutkus89.android.wvware.application"
        minSdkVersion 16
        targetSdkVersion 32
        versionCode 22
        versionName '1.2.6'
    }
    splits {
        abi {
            // Enables building multiple APKs per ABI.
            enable true

            // Specifies that we do want to also generate a universal APK that includes all ABIs.
            universalApk true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions.abortOnError false
    buildTypes.release.minifyEnabled true

    if (System.getenv('APP_SIGNING_KEYFILE') && System.getenv('APP_SIGNING_ALIAS') && System.getenv('APP_SIGNING_PASS')) {
        signingConfigs {
            release {
                storeFile file(System.getenv('APP_SIGNING_KEYFILE'))
                storePassword System.getenv('APP_SIGNING_PASS')
                keyAlias System.getenv('APP_SIGNING_ALIAS')
                keyPassword System.getenv('APP_SIGNING_PASS')
            }
        }
        buildTypes.release.signingConfig signingConfigs.release
    }
}

dependencies {
    implementation 'com.viliussutkus89:wvware-android:1.2.7'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.activity:activity:1.4.0'

    implementation 'androidx.work:work-runtime:2.7.1'
    implementation 'androidx.work:work-multiprocess:2.7.1'
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'

    // AboutLibraries-v10 requires AndroidSDK-21
    implementation "com.mikepenz:aboutlibraries:8.9.4"
    // Material required by AboutLibraries
    implementation 'com.google.android.material:material:1.6.0'
}
