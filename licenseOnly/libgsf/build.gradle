plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89.licenseOnly'

android {
    compileSdk 32
    defaultConfig {
        minSdk 16
        targetSdk 32

        versionCode 1
        versionName '1.14.46'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId project.name
                version = android.defaultConfig.versionName

                pom {
                    name = project.name
                    description = 'The G Structured File Library'
                    url = 'https://gitlab.gnome.org/GNOME/libgsf/'
                    packaging = 'aar'
                    inceptionYear = '2002'
                    licenses {
                        license {
                            name = 'GPLv2'
                            url = 'https://gitlab.gnome.org/GNOME/libgsf/-/blob/LIBGSF_1_14_46/COPYING'
                            distribution = 'repo'
                        }
                        license {
                            name = 'lGPLv2.1'
                            url = 'https://gitlab.gnome.org/GNOME/libgsf/-/blob/LIBGSF_1_14_46/COPYING.LIB'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = 'https://gitlab.gnome.org/GNOME/libgsf'
                        connection = 'https://gitlab.gnome.org/GNOME/libgsf.git'
                    }
                    developers {
                        // Developer list obtained from:
                        // https://gitlab.gnome.org/GNOME/libgsf/-/blob/LIBGSF_1_14_46/MAINTAINERS
                        developer {
                            name = 'Jody Goldberg'
                            email = 'jody@gnome.org'
                            id = 'jody'
                        }
                        developer {
                            name = 'Morten Welinder'
                            email = 'terra@gnome.org'
                            id = 'mortenw'
                        }
                        developer {
                            name = 'Dom Lachowicz'
                            email = 'domlachowicz@gmail.com'
                            id = 'doml'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
