plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89.licenseOnly'

android {
    compileSdk 32
    defaultConfig {
        minSdk 16
        targetSdk 32

        versionCode 1
        versionName '0.4.1'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId project.name
                version = android.defaultConfig.versionName

                pom {
                    name = project.name
                    description = 'This is a trivial minimal library intended to act as a proxy for a dynamically loaded optional libintl'
                    url = 'https://github.com/ViliusSutkus89/proxy-libintl'
                    packaging = 'aar'
                    inceptionYear = '2008'
                    licenses {
                        license {
                            name = 'LGPLv2'
                            url = 'https://github.com/ViliusSutkus89/proxy-libintl/blob/0.2/COPYING'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = 'https://github.com/ViliusSutkus89/proxy-libintl'
                        connection = 'https://github.com/ViliusSutkus89/proxy-libintl.git'
                    }
                    developers {
                        // Developer list obtained from:
                        // https://github.com/ViliusSutkus89/proxy-libintl/blob/0.4.1/README.md
                        // https://github.com/ViliusSutkus89/proxy-libintl/graphs/contributors
                        developer {
                            name = 'Tor Lillqvist'
                            email = 'tml@iki.fi'
                        }
                        developer {
                            name = 'Geoffrey Wossum'
                        }
                        developer {
                            name = 'Nirbheek Chauhan'
                            id = 'nirbheek'
                        }
                        developer {
                            name = 'Amos Wenger'
                            id = 'fasterthanlime'
                        }
                        developer {
                            name = 'Aleksandr Mezin'
                            id = 'amezin'
                        }
                        developer {
                            name = 'Ole André Vadla Ravnås'
                            id = 'oleavr'
                        }
                        developer {
                            name = "Vilius Sutkus '89"
                            id = 'ViliusSutkus89'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
