plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89.licenseOnly'

android {
    compileSdk 32
    defaultConfig {
        minSdk 16
        targetSdk 32

        versionCode 1
        versionName '1.2.6'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    // License only dependencies. To add cpp dependencies to POM
    implementation 'com.viliussutkus89.licenseOnly:wvWare:1.2.9'

    // Dependencies of wvWare
    implementation 'com.viliussutkus89.licenseOnly:libgsf:1.14.49'
    implementation 'com.viliussutkus89.licenseOnly:libpng:1.6.37'
    implementation 'com.viliussutkus89.licenseOnly:libwmf:v0.2.12'

    // Dependencies of libgsf
    implementation 'com.viliussutkus89.licenseOnly:GLib:2.72.1'
    implementation 'com.viliussutkus89.licenseOnly:libxml:2.9.14'

    // Dependencies of libpng
    // none

    // Dependencies of libwmf
    implementation 'com.viliussutkus89.licenseOnly:FreeType:2.12.1'

    // Dependencies of GLib
    implementation 'com.viliussutkus89.licenseOnly:libiconv:1.16'
    implementation 'com.viliussutkus89.licenseOnly:libffi:3.3'
    implementation 'com.viliussutkus89.licenseOnly:proxy-libintl:0.2'

    // Dependencies of libxml
    // none

    // Dependencies of FreeType
    // none

    // Dependencies of libiconv
    // none

    // Dependencies of libffi
    // none

    // Dependencies of proxy-libintl
    // none
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId project.name
                version = android.defaultConfig.versionName

                pom {
                    name = project.name
                    description = 'Dependency tree for vwWare-Android'
                    url = 'https://github.com/ViliusSutkus89/vwWare-Android'
                    packaging = 'aar'
                    inceptionYear = '2022'
                    licenses {
                        license {
                            name = 'GPLv3'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = 'https://github.com/ViliusSutkus89/vwWare-Android'
                        connection = 'https://github.com/ViliusSutkus89/vwWare-Android.git'
                    }
                    developers {
                        developer {
                            id = "ViliusSutkus89"
                            name = "Vilius Sutkus '89"
                            email = "ViliusSutkus89@gmail.com"
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
