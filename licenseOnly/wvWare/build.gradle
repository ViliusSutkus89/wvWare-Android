plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89.licenseOnly'

android {
    compileSdk 32
    defaultConfig {
        minSdk 16
        targetSdk 32

        versionCode 1
        versionName '1.2.9'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId project.name
                version = android.defaultConfig.versionName

                pom {
                    name = project.name
                    description = 'Library to load and parse Word 2000, 97, 95 and 6 files'
                    url = 'http://wvware.sourceforge.net'
                    packaging = 'aar'
                    inceptionYear = '2000'
                    licenses {
                        license {
                            name = 'GPLv2'
                            url = 'http://www.gnu.org/copyleft/gpl.html'
                            distribution = 'manual'
                        }
                    }
                    scm {
                        url = 'https://sourceforge.net/projects/wvware/'
                        connection = 'https://sourceforge.net/projects/wvware/'
                    }
                    developers {
                        // Developer list obtained from:
                        // http://wvware.sourceforge.net/#dom
                        developer {
                            name = 'Dom Lachowicz'
                        }
                        developer {
                            name = 'Caol√°n McNamara'
                        }
                        developer {
                            name = 'Martin Vermeer'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}
