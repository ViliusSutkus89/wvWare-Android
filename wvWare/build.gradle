/*
 * wvWare/build.gradle
 *
 * wvWare-Android (https://github.com/ViliusSutkus89/wvWare-Android)
 * Android port of wvWare - library to load and parse Word 2000, 97, 95 and 6 files.
 *
 * Copyright (c) 2019 - 2022, 2024 ViliusSutkus89.com
 *
 * wvWare-Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 3 as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.viliussutkus89'

android {
    namespace "com.viliussutkus89.android.wvware"
    compileSdk 34
    defaultConfig {
        minSdk 21
        targetSdk 34

        archivesBaseName = rootProject.name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild.cmake.arguments '-DANDROID_STL=c++_shared'

        buildConfigField("String", "VERSION_NAME", "\"${rootProject.version}\"")
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    buildFeatures {
        prefab true
        buildConfig true
    }
    externalNativeBuild.cmake {
        path "src/main/cpp/CMakeLists.txt"
        version "3.22.1"
    }
    ndkVersion '26.3.11579264'
    buildTypes.release.externalNativeBuild.cmake.arguments "-DCMAKE_BUILD_TYPE=MinSizeRel"
    sourceSets.androidTest.assets.srcDirs = [ getProject().parent.getLayout().getProjectDirectory().dir("test/androidTestAssets") ]
}

dependencies {
    implementation("com.viliussutkus89.ndk.thirdparty:wvWare-ndk26-static:1.2.9-beta-2")
    implementation("androidx.annotation:annotation:1.8.2")
    implementation 'com.viliussutkus89:tmpfile-android:3.0.5'
    implementation("com.viliussutkus89:assetextractor-android:1.3.3")
    implementation 'com.getkeepsafe.relinker:relinker:1.4.5'

    androidTestImplementation("androidx.test.ext:junit:1.2.1")
    androidTestImplementation("androidx.test:rules:1.6.1")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.6.1")
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }

            artifactId rootProject.name
            version = rootProject.version

            pom {
                name = rootProject.name
                description = 'wvWare (wvHtml) library port for Android'
                url = 'https://github.com/ViliusSutkus89/wvWare-Android'
                packaging = 'aar'
                inceptionYear = '2019'
                developers {
                    developer {
                        id = "ViliusSutkus89"
                        name = "Vilius Sutkus"
                        email = "ViliusSutkus89@gmail.com"
                    }
                }
                scm {
                    url = 'https://github.com/ViliusSutkus89/wvWare-Android'
                    connection = 'https://github.com/ViliusSutkus89/wvWare-Android.git'
                }
                licenses {
                    license {
                        name = 'GPLv3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

if (System.getenv('SIGNING_KEY')) {
    signing {
        required { true }
        useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
        sign publishing.publications.release
    }
}
