/*
 * wvWare/build.gradle
 *
 * Copyright (c) 2019 - 2021 Vilius Sutkus <ViliusSutkus89@gmail.com>
 *
 * wvWare-Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 3 as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

group = 'com.viliussutkus89'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30

        versionCode 21
        versionName project.version

        archivesBaseName = rootProject.name

        ndk.abiFilters = project.hasProperty('abi') ? [ project.property('abi') ] : null

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        sourceSets.main.assets.srcDirs = [ "${projectDir}/../dependency-builder/build/assets/" ]
    }
    //#UNCOMMENT-DURING-STAGING# /*
    buildTypes.release.externalNativeBuild {
      cmake.arguments '-DCMAKE_BUILD_TYPE=MinSizeRel'
    }
    externalNativeBuild.cmake {
        path "src/main/cpp/CMakeLists.txt"
        version "3.10.2"
    }
    ndkVersion "22.0.7026061"
    //#UNCOMMENT-DURING-STAGING# */
    buildFeatures.prefab true
}

configurations {
    javadocDependenciesToObtainClasspath
}

dependencies {
    //#UNCOMMENT-DURING-STAGING# /*
    compileOnly project(":dependency-builder")
    //#UNCOMMENT-DURING-STAGING# */
    implementation 'androidx.annotation:annotation:1.2.0'

    implementation 'com.viliussutkus89:tmpfile-android:3.0.4'
    implementation 'com.viliussutkus89:assetextractor-android:1.3.2'

    javadocDependenciesToObtainClasspath 'androidx.annotation:annotation:1.2.0'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:tmpfile-android:3.0.4'
    javadocDependenciesToObtainClasspath 'com.viliussutkus89:assetextractor-android:1.3.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //#UNCOMMENT-DURING-STAGING#implementation 'com.viliussutkus89:wvware-android:#STAGING-VERSION#'
    // The line above is used by the scripts/prepareLibraryForStagingRepository
}

publish.dependsOn ":dependency-builder:gatherLicenses"
publish.dependsOn ":javadocJar"
publish.dependsOn ":sourceJar"

task sourceJar(type: Jar) {
    into ("", { from android.sourceSets.main.java.srcDirs} )
    into ("", { from file("../LICENSE")})
    into ("cpp", { from fileTree(dir: "src/main/cpp/") } )
    into ("dependency-builder", { from fileTree(dir: "../dependency-builder/src/main/cpp/") } )
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDependenciesToObtainClasspath
    title = "${rootProject.name} ${project.version} API"
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

//publishing{
//    publications {
//        projectRelease(MavenPublication) {
//            artifactId rootProject.name
//            artifact "$buildDir/outputs/aar/${rootProject.name}-release.aar"
//            artifact sourceJar
//            artifact javadocJar
//            pom {
//            }
//            pom.withXml {
//                def node = asNode()
//
//                def licensesNode = node.appendNode("licenses")
//                def thisProjLicense = licensesNode.appendNode('license')
//
//                thisProjLicense.appendNode('name', 'wvWare-Android')
//                thisProjLicense.appendNode('url', "wvWare-Android/LICENSE\n\n" + new File("$projectDir/../LICENSE").getText('UTF-8') + "\n\n")
//                thisProjLicense.appendNode('distribution', 'repo')
//
//                new XmlSlurper().parse("$projectDir/../dependency-builder/build/licenses.xml").license.each {
//                    def license = licensesNode.appendNode('license')
//                    license.appendNode('name', it.name)
//                    license.appendNode('url', it.url)
//                    if (it.distribution) {
//                        license.appendNode('distribution', it.distribution)
//                    }
//                }
//
//                def dependenciesNode = node.appendNode('dependencies')
//                // Iterate over the implementation dependencies (we don't want the test ones),
//                // adding a <dependency> node for each
//                configurations.implementation.allDependencies.each {
//                    // Ensure dependencies such as fileTree are not included in the pom.
//                    if (it.name != 'unspecified') {
//                        def dependencyNode = dependenciesNode.appendNode('dependency')
//                        dependencyNode.appendNode('groupId', it.group)
//                        dependencyNode.appendNode('artifactId', it.name)
//                        dependencyNode.appendNode('version', it.version)
//                    }
//                }
//            }
//        }
//    }
//}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifactId rootProject.name
                version = rootProject.version

                artifact sourceJar
                artifact javadocJar

                pom {
                    name = rootProject.name
                    description = 'wvWare-Android'
                    url = 'https://github.com/ViliusSutkus89/wvWare-Android'
                    packaging = 'aar'
                    inceptionYear = '2019'
                    licenses {
                        license {
                            name = 'GPL-3.0-only'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = "ViliusSutkus89"
                            name = "Vilius Sutkus"
                            email = "ViliusSutkus89@gmail.com"
                        }
                    }
                    scm {
                        url = 'https://github.com/ViliusSutkus89/wvWare-Android'
                        connection = 'https://github.com/ViliusSutkus89/wvWare-Android.git'
                    }
                }
            }
        }
    }

    // This may look ugly...
    // but useInMemoryPgpKeys expects valid keys even with required { false }
    if (System.getenv('SIGNING_KEY')) {
        signing {
            required { true }
            useInMemoryPgpKeys(System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASS'))
            sign publishing.publications.release
        }
    }
}

//task extractLibtmpfileSoForLinkingInCMake {
//    doLast {
//        def tmpfileandroid = configurations.releaseRuntimeClasspath.getResolvedConfiguration().getResolvedArtifacts().find {
//            it.name.startsWith("tmpfile-android")
//        }
//        copy {
//            from zipTree(tmpfileandroid.getFile())
//            into "${project.buildDir}/tmpfile/"
//            include "jni/**/libtmpfile.so"
//        }
//    }
//}
//
//preBuild.dependsOn extractLibtmpfileSoForLinkingInCMake
